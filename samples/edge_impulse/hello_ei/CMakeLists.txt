#
# Copyright (c) 2026 Nordic Semiconductor ASA
#
# SPDX-License-Identifier: LicenseRef-Nordic-5-Clause
#

cmake_minimum_required(VERSION 3.20.0)

find_package(Zephyr REQUIRED HINTS $ENV{ZEPHYR_BASE})
project(hello_ei)

# Add application source files
target_sources(app PRIVATE
  src/main.c
)

# Add application include directories
target_include_directories(app PRIVATE
  src/include
)

# Enable C linkage for Edge Impulse module (allows calling from C code)
zephyr_compile_definitions(
  EI_C_LINKAGE=1
  EIDSP_SIGNAL_C_FN_POINTER=1
)

# Suppress warnings generated by Edge Impulse SDK
zephyr_compile_options(
  -Wno-double-promotion
  -Wno-unused
  -Wno-sign-compare
  -Wno-maybe-uninitialized
)

# Expand any ${VARIABLES} in the path
string(CONFIGURE ${CONFIG_EDGE_IMPULSE_PATH} CONFIG_EDGE_IMPULSE_PATH)

# Convert relative paths to absolute paths
if(NOT IS_ABSOLUTE ${CONFIG_EDGE_IMPULSE_PATH})
  # Using application source directory as base directory for relative path
  set(CONFIG_EDGE_IMPULSE_PATH ${APPLICATION_SOURCE_DIR}/${CONFIG_EDGE_IMPULSE_PATH})
endif()

set(FETCH_CONTENT_NAME edge_impulse)

include(FetchContent)

FetchContent_Declare(
    ${FETCH_CONTENT_NAME}
    URL ${CONFIG_EDGE_IMPULSE_PATH}
)

FetchContent_MakeAvailable(${FETCH_CONTENT_NAME})
